import java.time.ZonedDateTime
import java.time.format.DateTimeFormatter
import java.time.temporal.ChronoUnit

plugins {
	// gradle dependencyUpdates -Drevision=release
	id "com.github.ben-manes.versions" version "0.27.0"
	id "com.github.johnrengelman.shadow" version "5.2.0"
	id 'com.palantir.git-version' version "0.12.2"
	id 'java'
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'idea'

sourceCompatibility = 12
targetCompatibility = 12
compileJava.options.encoding = 'UTF-8'

group = "chrisliebaer"
version gitVersion()
mainClassName = "chrisliebaer.chrisliebot.Chrisliebot"

// set up generation of ressources
def generatedResources = "$buildDir/generated-resources/main"
sourceSets.main.output.dir generatedResources, builtBy: "generateResources"

task generateResources() {
	dependsOn "generateBuildInfo"
}

task generateBuildInfo() {
	doLast() {
		def det = versionDetails()
		new File(generatedResources).mkdirs()
		def f = new File(generatedResources, "vcs.properties")
		f.createNewFile()
		f.text = """
version: ${project.version}
commit: ${det.gitHashFull}
branch: ${det.branchName}
clean: ${det.isCleanTag}
buildTime: ${DateTimeFormatter.ISO_ZONED_DATE_TIME.format(ZonedDateTime.now().truncatedTo(ChronoUnit.SECONDS))}
"""
	}
}

repositories {
	mavenLocal()
	mavenCentral()
	jcenter()
}

dependencies {
	compileOnly "org.projectlombok:lombok:1.18.12"
	annotationProcessor "org.projectlombok:lombok:1.18.12"

	// beans
	implementation 'javax.validation:validation-api:2.0.1.Final'
	implementation 'org.hibernate.validator:hibernate-validator:6.1.0.Final'
	implementation 'org.hibernate.validator:hibernate-validator-annotation-processor:6.1.0.Final'
	implementation 'org.glassfish:javax.el:3.0.1-b11'

	// logging
	def slf4j = "1.7.30"
	def log4j2 = "2.13.0"
	implementation "org.slf4j:slf4j-api:$slf4j"
	implementation "org.apache.logging.log4j:log4j-core:$log4j2"
	implementation "org.apache.logging.log4j:log4j-slf4j-impl:$log4j2"
	implementation 'com.djdch.log4j:log4j-staticshutdown:1.1.0'

	// commons stuff you don't know you actually want until you realise it's missing
	implementation group: 'commons-io', name: 'commons-io', version: '2.6'
	implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.9'
	implementation group: 'org.apache.commons', name: 'commons-text', version: '1.8'
	implementation "com.google.code.gson:gson:2.8.6"
	implementation "com.google.guava:guava:28.2-jre"

	// retrofit
	implementation "com.squareup.retrofit2:retrofit:2.7.1"
	implementation "com.squareup.retrofit2:converter-gson:2.7.1"

	// okhttp
	implementation 'com.squareup.okhttp3:okhttp:4.3.1'
	implementation 'com.squareup.okhttp3:logging-interceptor:4.3.1'

	// webserver
	//implementation 'io.undertow:undertow-core:2.0.29.Final'

	// the only fuckling exec library that doesn't suck fucking donkey balls
	implementation 'org.zeroturnaround:zt-exec:1.11'

	// features
	implementation 'com.joestelmach:natty:0.13'
	implementation 'dnsjava:dnsjava:2.1.9'
	implementation 'org.nibor.autolink:autolink:0.10.0'
	implementation 'org.jsoup:jsoup:1.12.1'
	implementation 'org.mariadb.jdbc:mariadb-java-client:2.5.3'
	implementation 'me.xdrop:fuzzywuzzy:1.2.0'

	// protocols
	implementation 'org.kitteh.irc:client-lib:7.2.2'
	implementation('net.dv8tion:JDA:4.1.0_96') {
		exclude module: 'opus-java'
	}
}

idea {
	module {
		downloadJavadoc = true
		downloadSources = true
	}
}
